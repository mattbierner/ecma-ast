/**
 * @fileOverview Data structures used to track locations in ECMAScript source
 *   text.
 */
package (
    SourcePosition
    SourceLocation)
{

/* Positions
 ******************************************************************************/
// SourcePosition
////////////////////////////////////////
/**
 * Data structure for a point in source code.
 */
SourcePosition = function\line column file =self-> {
    self.line = line;
    self.column = column;
    self.file = file;
};

/**
 * Initial SourcePosition in a stream.
 */
SourcePosition.initial = new SourcePosition(1, 0, null);

SourcePosition.prototype.increment = \tok =self->
    ?tok === '\n'
        :new SourcePosition(self.line + 1, 0, self.file)
        :new SourcePosition(self.line, self.column + 1, self.file);

SourcePosition.prototype.toString = \ =self->
    "{line:" + self.line + " col:" + self.column + "}";

SourcePosition.prototype.compare = \pos =self->
    ?self.line === pos.line
        :self.column - pos.column
        :self.line - pos.line;

// SourceLocation
////////////////////////////////////////
/**
 * Location in source code.
 * 
 * Locations are a range of characters between two positions.
 */
SourceLocation = function \start end file =self-> {
    self.start = start;
    self.end = end;
    self.file = file;
};

SourceLocation.prototype.toString = \ =self->
    '{' + self.file + ' start:' + self.start + ' end:' + self.end + '}';

/**
 * 
 */
SourceLocation.merge = \s1 s2 ->
    new SourceLocation(
        ?s1.start.compare(s2.start) > 0 : s2.start : s1.start,
        ?s1.end.compare(s2.end) > 0 : s1.end : s2.end,
        s1.file || s2.file);

}